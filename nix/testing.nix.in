{ nixpkgs ? <nixpkgs>
, system ? builtins.currentSystem
, dysnomia ? builtins.storePath "@dysnomia@/../.."
, disnix ? builtins.storePath @DISNIX_PREFIX@
, disnixos ? builtins.storePath @prefix@
}:

let
  pkgs = import nixpkgs {};

  generateManifestSrc =
    {name, tarball}:

    pkgs.stdenv.mkDerivation {
      name = "${name}-manifest-src";
      buildCommand =
        ''
          mkdir -p $out
          cd $out
          tar xfvj ${tarball}/tarballs/*.tar.bz2 --strip-components=1
        '';
    };
in
{
  sourceTarball =
    {name, version, src, officialRelease}:

    pkgs.releaseTools.sourceTarball {
      inherit name version src officialRelease;

      distPhase =
        ''
          mkdir ../$name
          rm -Rf `find . -name .svn`
          mv * ../$name
          cd ..
          mkdir -p $out/tarballs
          tar cfvj $out/tarballs/$name.tar.bz2 $name
        '';
    };

  buildManifest =
    {name, version, tarball, servicesFile, networkFile, externalNetworkFile ? false, distributionFile, extraParams ? {}, externalDistributionFile ? false}:

    let
      infrastructure = import ./generate-infrastructure.nix {
        networkFiles = [ (if externalNetworkFile then networkFile else "${manifestSrc}/${networkFile}") ];
        useVMTesting = true;
        useBackdoor = true;
        inherit nixpkgs dysnomia;
      };

      manifestSrc = generateManifestSrc {
        inherit name tarball;
      };

      manifestModule = import "${disnix}/share/disnix/manifest.nix";
    in
    pkgs.releaseTools.nixBuild {
      inherit name version;
      src = tarball;

      manifest = manifestModule.generateManifestFromModels {
        servicesFile = "${manifestSrc}/${servicesFile}";
        infrastructureFile = "${infrastructure}";
        distributionFile = if externalDistributionFile then distributionFile else "${manifestSrc}/${distributionFile}";
        targetProperty = "backdoor";
        clientInterface = "${disnixos}/bin/disnix-backdoor-client";
        inherit nixpkgs extraParams;
      };

      installPhase =
        ''
          mkdir -p $out
          ln -s $manifest $out/manifest.xml
        '';

      checkPhase = "true";
    };

  disnixTest =
    {name, manifest, tarball, networkFile, externalNetworkFile ? false, testScript, dysnomiaStateDir ? "", postActivateTimeout ? 1}:

    let
      manifestSrc = generateManifestSrc {
        inherit name tarball;
      };

      network = if externalNetworkFile then import networkFile else import "${manifestSrc}/${networkFile}";
    in
    with import "${nixpkgs}/nixos/lib/testing-python.nix" { inherit system; };

    simpleTest {
      nodes = network;

      testScript = import ./generate-testscript.nix {
        inherit network testScript disnix dysnomiaStateDir postActivateTimeout;
        inherit (pkgs) socat;
        inherit (pkgs.lib) concatMapStrings;
        manifestFile = "${manifest}/manifest.xml";
      };
    };
}
